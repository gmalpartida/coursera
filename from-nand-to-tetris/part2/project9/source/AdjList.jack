/***************************************************************************

Class AdjList
Data structure used to represent an undirected graph.

Gino Malpartida
***************************************************************************/


class AdjList
{
    field Array vertices;
    field int vertex_count;

	/**************************************************
	constructor: creates a new instance of an adjacency
				 list.
				 allocates memory to fields.
	**************************************************/
    constructor AdjList new(int num_vertices)
    {
        var int index;
        
        let vertices = Array.new(num_vertices);
        
        let index = 0;
        while (index < num_vertices)
        {
            let vertices[index] = null;
            let index = index + 1;
        }

        let vertex_count = num_vertices;

        return this;
    }

	/**************************************************
	dispose: deallocates memory used.
	**************************************************/
    method void dispose()
    {
        do vertices.dispose();
        return;
    }

	/***************************************************
	print: prints contents of adjacency list.
			used for debugging.
	***************************************************/
    method void print(CityMap Cities)
    {
        var LinkedList cur;
        var int index;
        let index = 0;
        while ((index < vertex_count))
        {
            do Output.printString("from: ");
            do Output.printString(Cities.get_name(index));
            do Output.printString(" to:");
            do Output.println();
            let cur = vertices[index];
            do cur.print(Cities);
            do Output.println();
            let index = index + 1;
        }

        return;
    }

	/****************************************************************
	add_edge: adds an edge between two vertices.
			  the edge is added in both directions.
	****************************************************************/
    method void add_edge(int orig, int dest, NodeData data)
    {
        var LinkedList cur;
        let cur = vertices[orig];
        if (null = cur)
        {
            let vertices[orig] = LinkedList.new(data, null);
        }
        if (~(null = cur))
        {
            let vertices[orig] = LinkedList.new(data, cur);
        }
        let cur = vertices[dest];
        if (null = cur)
        {
            let vertices[dest] = LinkedList.new(NodeData.new(orig, data.get_weight()), null);
        }
        if (~(null = cur))
        {
            let vertices[dest] = LinkedList.new(NodeData.new(orig, data.get_weight()), cur);
        }
        return;
    }

	/******************************************************************
	get_edges: retrieves the edges for a vertex.
	******************************************************************/
	method Array get_edges(int vertex)
	{
		return vertices[vertex];
	}





}

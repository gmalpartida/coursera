class LinkedList
{
    field NodeData data;
    field LinkedList next;

    constructor LinkedList new(NodeData new_data, LinkedList new_next)
    {
        let data = new_data;
        let next = new_next;
        return this;
    }

    method void dispose()
    {
        var LinkedList prev, cur;
        let prev = null;
        let cur = this;
        while (~(cur = null))
        {
            let prev = cur;
            let cur = cur.getNext();
            do Memory.deAlloc(prev);
        }
        return;
    }

    method NodeData getData()
    {
        return data;
    }

    method LinkedList getNext()
    {
        return next;
    }

    method void print(CityMap Cities)
    {
        var NodeData data;
        var LinkedList cur;
        let cur = this;
        while (~(cur = null))
        {
            let data = cur.getData();
            do data.print(Cities);
            do Output.println();
            let cur = cur.getNext();
        }
        return;
    }
    method void setData(NodeData new_data)
    {
        let data = new_data;
        return;
    }
    method void setNext(LinkedList new_next)
    {
        let next = new_next;
        return;
    }
}


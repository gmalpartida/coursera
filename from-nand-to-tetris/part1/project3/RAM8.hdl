// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/RAM8.hdl
/**
 * Memory of eight 16-bit registers.
 * If load is asserted, the value of the register selected by
 * address is set to in; Otherwise, the value does not change.
 * The value of the selected register is emitted by out.
 */
CHIP RAM8 {
    IN in[16], load, address[3];
    OUT out[16];

    PARTS:
    // select the register specified by address
    DMux8Way(in=load,sel=address,a=tmpa,b=tmpb,c=tmpc,
                d=tmpd,e=tmpe,f=tmpf,g=tmpg,h=tmph);

    // set the register to the new in value if 
    // corresponding bit is equal to 1
    Register(in=in,load=tmpa,out=outa);
    Register(in=in,load=tmpb,out=outb);
    Register(in=in,load=tmpc,out=outc);
    Register(in=in,load=tmpd,out=outd);
    Register(in=in,load=tmpe,out=oute);
    Register(in=in,load=tmpf,out=outf);
    Register(in=in,load=tmpg,out=outg);
    Register(in=in,load=tmph,out=outh);

    // output new register value
    Mux8Way16(a=outa,b=outb,c=outc,d=outd,
    e=oute,f=outf,g=outg,h=outh,sel=address,out=out);
}